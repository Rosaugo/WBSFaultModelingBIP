@cpp(include="stdio.h")
package WBS_Fault_ARCH1

	// extern function printf for output
	extern function printf (string, int)
	extern function printf (string, int, int)
	extern function printf (string, int, int, int, bool)
	extern function printf (string, bool)
	extern function printf (string)
	extern function int max(int, int)

	// Constant data definitions 

	// Port types definitions
	port type WBSPort_t1 ( int var )
	port type WBSPort_t2 ( bool var )
	port type WBSPort_t3 ( int var1, int var2 )
	port type Internal_Port ()

	// Connector types definitions

	// Add a connector type for internal Shutoff valve and Meter valve fault model
	connector type transFaultManageSig( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3, WBSPort_t1 p4 )
		define p1 p2 p3 p4
		on p1 p2 p3 p4 down { p2.var = p1.var; p3.var = p1.var; p4.var = p1.var; }
	end

	connector type transFaultManageSig_v2( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3 )
		define p1 p2 p3
		on p1 p2 p3 down { p2.var = p1.var; p3.var = p1.var; }
	end

	connector type transFaultManageSig_v3( WBSPort_t2 p1, WBSPort_t2 p2, WBSPort_t2 p3 )
		define p1 p2 p3
		on p1 p2 p3 down { p2.var = p1.var; p3.var = p1.var; }
	end

	connector type transHyd_v2 ( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3 )
		define p1 p2 p3
		on p1 p2 p3 down
			                  {
				        if(p1.var > p2.var) then
					p3.var = p1.var;
				        else
					p3.var = p2.var;
				        fi
				}
	end

	connector type transHyd( WBSPort_t1 p1, WBSPort_t1 p2 )
		define p1 p2
		on p1 p2 down { p2.var = p1.var; }
	end

	connector type transHyd_Fault( WBSPort_t1 p1, WBSPort_t1 p2 )
		define p1' p2
		on p1 p2 down { p2.var = p1.var; }
	end

	connector type transHyd_max( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3 )
		define p1 p2 p3
		on p1 p2 p3 down { p3.var = max(p1.var, p2.var); }
	end

	connector type transCmd( WBSPort_t2 p1, WBSPort_t2 p2 )
		define p1 p2
		on p1 p2 down { p2.var = p1.var; }
	end

	// Fault model for BSCU Module One
	connector type sendBSCUFault( WBSPort_t2 p1, WBSPort_t2 p2)
		data bool signal
		export port WBSPort_t2 sendSignal(signal)
		define p1' p2'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
	end

	// Fault model for BSCU Module Two
	connector type getPWR( WBSPort_t2 p1)
		data bool signal
		export port WBSPort_t2 receiveSignal(signal)
		define p1'
		on p1 down { p1.var = signal; }
	end

	connector type getSensor ( WBSPort_t1 p1)
		data int signal
		export port WBSPort_t1 receiveSignal(signal)
		define p1'
		on p1 down { p1.var = signal; }
	end

	// Fault model for Power Supply
	connector type sendPWR( WBSPort_t2 p1, WBSPort_t2 p2)
		data bool signal
		export port WBSPort_t2 sendSignal(signal)
		define p1' p2'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
	end

	// Fault model for Hydraulic Pump
	connector type sendHydraulic( WBSPort_t1 p1, WBSPort_t1 p2)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
	end

	// Fault model for Shutoff Valve
	connector type getSYSVal( WBSPort_t2 p1, WBSPort_t2 p2, WBSPort_t2 p3)
		data bool signal
		export port WBSPort_t2 receiveSignal(signal)
		define p1' p2' p3'
		on p1 down { p1.var = signal; printf("signal received is %d\n", signal);}
		on p2 down { p2.var = signal; printf("signal received is %d\n", signal);}
		on p3 down { p3.var = signal; printf("signal received is %d\n", signal);}
	end

	connector type sendSOHyd( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2' p3'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
		on p3 up { signal = p3.var; }
	end

	connector type sendASHyd( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2' p3'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
		on p3 up { signal = p3.var; }
	end

	connector type getHydraulic( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2' p3'
		on p1 down { p1.var = signal; printf("signal received is %d\n", signal);}
		on p2 down { p2.var = signal; printf("signal received is %d\n", signal);}
		on p3 down { p3.var = signal; printf("signal received is %d\n", signal);}
	end

	// Fault model for Meter Valve
	connector type getBRKASCMD( WBSPort_t2 p1, WBSPort_t2 p2, WBSPort_t2 p3)
		data bool signal
		export port WBSPort_t2 receiveSignal(signal)
		define p1' p2' p3'
		on p1 down { p1.var = signal; printf("signal received is %d\n", signal);}
		on p2 down { p2.var = signal; printf("signal received is %d\n", signal);}
		on p3 down { p3.var = signal; printf("signal received is %d\n", signal);}
	end

	connector type sendMeterHyd( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2' p3'
		on p1 up { signal = p1.var; }
		on p2 up { signal = p2.var; }
		on p3 up { signal = p3.var; }
	end

	connector type getSOHyd( WBSPort_t1 p1, WBSPort_t1 p2, WBSPort_t1 p3)
		data int signal
		export port WBSPort_t1 sendSignal(signal)
		define p1' p2' p3'
		on p1 down { p1.var = signal; printf("signal received is %d\n", signal);}
		on p2 down { p2.var = signal; printf("signal received is %d\n", signal);}
		on p3 down { p3.var = signal; printf("signal received is %d\n", signal);}
	end

	connector type sendSYSVal ( WBSPort_t2 p1 )
		data bool signal
		export port WBSPort_t2 sendSignal(signal)
		define p1'
		on p1 up { signal = p1.var; }
	end

	connector type sendBRKASCMD ( WBSPort_t2 p1 )
		data bool signal
		export port WBSPort_t2 sendSignal(signal)
		define p1'
		on p1 up { signal = p1.var; }
	end

	connector type sendAntiSkidCmd ( WBSPort_t2 p1 )
		data bool signal
		export port WBSPort_t2 sendSignal(signal)
		define p1'
		on p1 up { signal = p1.var; }
	end

	// Atom types definitions
	atom type HydraulicPump()
		// Variable declarations
		data int Hydraulic
		data int ManageSignal

		// Port declarations
		export port WBSPort_t1 sendHydraulic ( Hydraulic )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { Hydraulic = 0; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 0)
			do { Hydraulic = 10; }

		internal from Init2 to Init
			provided(ManageSignal != 0)

		on sendHydraulic from Exec to Init

		// invariants

		// Priorities
	end

	atom type HydraulicPump_Loss()
		// Variable declarations
		data int Hydraulic
		data int ManageSignal

		// Port declarations
		export port WBSPort_t1 sendHydraulic ( Hydraulic )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { Hydraulic = 0; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 1)
			do{ Hydraulic = 0; }

		internal from Init2 to Init
			provided(ManageSignal != 1)

		on sendHydraulic from Exec to Init

		// invariants

		// Priorities
	end

	atom type ShutoffValve()
		// Variable declarations
		data int ShutOffHyd
		data int Hydraulic
		data int ManageSignal
		data bool SystemValidity

		// Port declarations
		export port WBSPort_t2 receiveSYSVal ( SystemValidity )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendSOHyd ( ShutOffHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, R1,Exec, S1, S2, Init2, Init3

		// Initial location declarations
		initial to Init do { ShutOffHyd = 0; SystemValidity = false; Hydraulic = 0; ManageSignal = 0; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 0)
			do {printf("ShutoffValve one :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 0)
			do {printf("ShutoffValve one :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}

		on receiveSYSVal from Init3 to R1
			do {printf("ShutoffValve one :SystemValidity received is %d, trans from Init3 to R1\n", SystemValidity);}

		on receiveHydraulic from R1 to Exec
			do {printf("ShutoffValve one :Hydraulic received is %d, trans from R1 to Exec\n", Hydraulic);}

		internal from Exec to S1
			provided(SystemValidity == true)
			do{ShutOffHyd = Hydraulic; printf("In ShutoffValve one, ShutoffHyd is %d, Hydraulic is %d, trans from Exec to S1\n",ShutOffHyd,Hydraulic);}

		internal from Exec to S2
			provided(SystemValidity == false)
			do{ShutOffHyd = 0;printf("In ShutoffValve one, ShutoffHyd is %d, Hydraulic is %d, trans from Exec to S2\n",ShutOffHyd,Hydraulic);}

		on sendSOHyd from S1 to Init
			do {printf("ShutoffValve one :ShutOffHyd is %d, trans from S1 to Init\n", ShutOffHyd);}

		on sendSOHyd from S2 to Init
			do {printf("ShutoffValve one :ShutOffHyd is %d, trans from S2 to Init\n",ShutOffHyd);}

		// invariants

		// Priorities
	end

	// For fault model
	atom type ShutoffValve_StuckOpen()
		// Variable declarations
		data int ShutOffHyd
		data int Hydraulic
		data int ManageSignal
		data bool SystemValidity

		// Port declarations
		export port WBSPort_t2 receiveSYSVal ( SystemValidity )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendSOHyd ( ShutOffHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, R1,Exec, S1, S2, Init2, Init3

		// Initial location declarations
		initial to Init do { ShutOffHyd = 0; SystemValidity = false; Hydraulic = 0; ManageSignal = 0; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 2)
			do {printf("ShutoffValve two :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 2)
			do {printf("ShutoffValve two :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}

		on receiveSYSVal from Init3 to R1
			do {printf("ShutoffValve two :SystemValidity received is %d, trans from Init3 to R1\n", SystemValidity);}

		on receiveHydraulic from R1 to Exec
			do {printf("ShutoffValve two :Hydraulic received is %d, trans from R1 to Exec\n", Hydraulic);}

		internal from Exec to S1
			provided(SystemValidity == true)
			do{ShutOffHyd = Hydraulic;}

		internal from Exec to S2
			provided(SystemValidity == false)
			do{ShutOffHyd = Hydraulic;}

		on sendSOHyd from S1 to Init
			do {printf("ShutoffValve two :ShutOffHyd is %d, trans from S1 to Init\n", ShutOffHyd);}

		on sendSOHyd from S2 to Init
			do {printf("ShutoffValve two :ShutOffHyd is %d, trans from S2 to Init\n",ShutOffHyd);}

		// invariants

		// Priorities
	end

	// For fault model
	atom type ShutoffValve_StuckClose()
		// Variable declarations
		data int ShutOffHyd
		data int Hydraulic
		data int ManageSignal
		data bool SystemValidity

		// Port declarations
		export port WBSPort_t2 receiveSYSVal ( SystemValidity )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendSOHyd ( ShutOffHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, R1,Exec, S1, S2, Init2, Init3

		// Initial location declarations
		initial to Init do { ShutOffHyd = 0; SystemValidity = false; Hydraulic = 0; ManageSignal = 0; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 1)
			do {printf("ShutoffValve three :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 1)
			do {printf("ShutoffValve three :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}

		on receiveSYSVal from Init3 to R1
			do {printf("ShutoffValve three :SystemValidity received is %d, trans from Init3 to R1\n", SystemValidity);}

		on receiveHydraulic from R1 to Exec
			do {printf("ShutoffValve three :Hydraulic received is %d, trans from R1 to Exec\n", Hydraulic);}

		internal from Exec to S1
			provided(SystemValidity == true)
			do{ShutOffHyd = 0;}

		internal from Exec to S2
			provided(SystemValidity == false)
			do{ShutOffHyd = 0;}

		on sendSOHyd from S1 to Init
			do {printf("ShutoffValve three :ShutOffHyd is %d, trans from S1 to Init\n", ShutOffHyd);}

		on sendSOHyd from S2 to Init
			do {printf("ShutoffValve three :ShutOffHyd is %d, trans from S2 to Init\n",ShutOffHyd);}

		// invariants

		// Priorities
	end

	atom type Valve_Manager()
		// Variable declarations
		data int ManageSignal

		// Port declarations
		export port WBSPort_t1 sendManager ( ManageSignal )
		port Internal_Port NormalToE1()
		port Internal_Port NormalToE2()
		port Internal_Port E1ToNormal()
		port Internal_Port E2ToNormal()
		port Internal_Port E1ToE2()
		port Internal_Port E2ToE1()

		// Clock declarations

		// Locations declaration
		place Normal, E1, E2

		// Initial location declarations
		initial to Normal do { ManageSignal = 0; }

		// transitions
		on sendManager from Normal to Normal

		on sendManager from E1 to E1

		on sendManager from E2 to E2

		on NormalToE1 from Normal to E1
			do {ManageSignal = 1;}

		on NormalToE2 from Normal to E2
			do {ManageSignal = 2;}

		on E1ToNormal from E1 to Normal
			do {ManageSignal = 0;}

		on E2ToNormal from E2 to Normal
			do {ManageSignal = 0;}

		on E2ToE1 from E2 to E1
			do {ManageSignal = 1;}

		on E1ToE2 from E1 to E2
			do {ManageSignal = 2;}

		// invariants

		// Priorities
	end

	atom type MeterValve()
		// Variable declarations
		data int ShutOffHyd
		data int MeterHyd
		data bool BrakeAntiSkidCMD
		data bool MechanicalCMD
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveBRKASCMD ( BrakeAntiSkidCMD )
		export port WBSPort_t1 receiveSOHyd ( ShutOffHyd )
		export port WBSPort_t1 sendMeterHyd ( MeterHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, Init2, Init3, R1, Exec, S1, S2

		// Initial location declarations
		initial to Init do { MeterHyd = 0; MechanicalCMD = false; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 0)
			do {printf("MeterValve one :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 0)
			do {printf("MeterValve one :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}	

		on receiveBRKASCMD from Init3 to R1

		on receiveSOHyd from R1 to Exec

		internal from Exec to S1
			provided(BrakeAntiSkidCMD == true || MechanicalCMD == true )
			do{MeterHyd = ShutOffHyd;}	

		internal from Exec to S2
			provided(BrakeAntiSkidCMD == false && MechanicalCMD == false)
			do{MeterHyd = 0;}

		on sendMeterHyd from S1 to Init

		on sendMeterHyd from S2 to Init

		// invariants

		// Priorities
	end

	// Add two fault module for Meter Valve
	atom type MeterValve_StuckOpen()
		// Variable declarations
		data int ShutOffHyd
		data int MeterHyd
		data bool BrakeAntiSkidCMD
		data bool MechanicalCMD
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveBRKASCMD ( BrakeAntiSkidCMD )
		export port WBSPort_t1 receiveSOHyd ( ShutOffHyd )
		export port WBSPort_t1 sendMeterHyd ( MeterHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, Init2, Init3, R1, Exec, S1, S2

		// Initial location declarations
		initial to Init do { MeterHyd = 0; MechanicalCMD = false; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 2)
			do {printf("MeterValve two :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 2)
			do {printf("MeterValve two :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}	

		on receiveBRKASCMD from Init3 to R1

		on receiveSOHyd from R1 to Exec

		internal from Exec to S1
			provided(BrakeAntiSkidCMD == true || MechanicalCMD == true )
			do{MeterHyd = ShutOffHyd;}	

		internal from Exec to S2
			provided(BrakeAntiSkidCMD == false && MechanicalCMD == false)
			do{MeterHyd = ShutOffHyd;}

		on sendMeterHyd from S1 to Init

		on sendMeterHyd from S2 to Init

		// invariants

		// Priorities
	end

	atom type MeterValve_StuckClose()
		// Variable declarations
		data int ShutOffHyd
		data int MeterHyd
		data bool BrakeAntiSkidCMD
		data bool MechanicalCMD
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveBRKASCMD ( BrakeAntiSkidCMD )
		export port WBSPort_t1 receiveSOHyd ( ShutOffHyd )
		export port WBSPort_t1 sendMeterHyd ( MeterHyd )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Init, Init2, Init3, R1, Exec, S1, S2

		// Initial location declarations
		initial to Init do { MeterHyd = 0; MechanicalCMD = false; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Init3
			provided(ManageSignal == 1)
			do {printf("MeterValve three :ManageSignal is %d, trans from Init2 to Init3\n", ManageSignal);}

		internal from Init2 to Init
			provided(ManageSignal != 1)
			do {printf("MeterValve three :ManageSignal is %d, trans from Init2 to Init\n", ManageSignal);}	

		on receiveBRKASCMD from Init3 to R1

		on receiveSOHyd from R1 to Exec

		internal from Exec to S1
			provided(BrakeAntiSkidCMD == true || MechanicalCMD == true )
			do{MeterHyd = 0;}	

		internal from Exec to S2
			provided(BrakeAntiSkidCMD == false && MechanicalCMD == false)
			do{MeterHyd = 0;}

		on sendMeterHyd from S1 to Init

		on sendMeterHyd from S2 to Init

		// invariants

		// Priorities
	end

	atom type ValveManager_OneState()
		// Variable declarations
		data int ManageSignal

		// Port declarations
		export port WBSPort_t1 sendManager ( ManageSignal )
		port Internal_Port NormalToE1()
		port Internal_Port E1ToNormal()

		// Clock declarations

		// Locations declaration
		place Normal, E1

		// Initial location declarations
		initial to Normal do { ManageSignal = 0; }

		// transitions
		on sendManager from Normal to Normal

		on sendManager from E1 to E1

		on NormalToE1 from Normal to E1
			do { ManageSignal = 1; }

		on E1ToNormal from E1 to Normal
			do { ManageSignal = 0; }

		// invariants

		// Priorities
	end

	atom type BSCU_ModuleTwo()
		// Variable declarations
		data int WheelSpeed
		data int PedalPosition
		data int GroundSpeed
		data bool SystemValidity
		data bool BrakeAntiSkidCMD
		data bool Power
		data bool LossBrakeCMD
		data bool AntiSkidCmd

		// Port declarations
		export port WBSPort_t1 receiveGSpeed ( GroundSpeed )
		export port WBSPort_t1 receiveWSpeed ( WheelSpeed )
		export port WBSPort_t1 receivePP ( PedalPosition )
		export port WBSPort_t2 sendSYSVal ( SystemValidity )
		export port WBSPort_t2 sendBRKASCMD ( BrakeAntiSkidCMD )
		export port WBSPort_t2 receivePWR( Power )
		export port WBSPort_t2 receiveBSCUFault ( LossBrakeCMD )
		export port WBSPort_t2 sendAntiSkidCmd ( AntiSkidCmd )
		port Internal_Port Exec3ToAS1()
		port Internal_Port Exec3ToAS2()

		// Clock declarations

		// Locations declaration
		place Exec1, R0, R1, R2, R3, P1, P2, Exec2, Exec3, AS1, AS2, AS3

		// Initial location declarations
		initial to Exec1 do { BrakeAntiSkidCMD = false; SystemValidity = false; Power = true; AntiSkidCmd = false;}

		// transitions
		on receivePWR from Exec1 to R0
			do { printf("Fault trace: BSCU receive power\n"); }

		on receiveGSpeed from R0 to R1
			do { printf("Fault trace: BSCU receive ground speed\n"); }

		on receiveWSpeed from R1 to R2
			do { printf("Fault trace: BSCU receive wheel speed\n"); }

		on receivePP from R2 to R3
			do { printf("Fault trace: BSCU receive pedal position\n"); }

		internal from R3 to P1
			provided(Power == true)
			do{SystemValidity = true;}

		internal from R3 to P2
			provided(Power == false)
			do{SystemValidity = false;}

		on sendSYSVal from P1 to Exec2
			do { printf("Fault trace: BSCU send system validity\n"); }

		on sendSYSVal from P2 to Exec2
			do { printf("Fault trace: BSCU send system validity\n"); }

		on receiveBSCUFault from Exec2 to Exec3
			do { printf("Fault trace: BSCU receive BSCU fault\n"); }

		on Exec3ToAS1 from Exec3 to AS1
			provided(PedalPosition == 0 || WheelSpeed != GroundSpeed || LossBrakeCMD)
			do{BrakeAntiSkidCMD = false; AntiSkidCmd = false;}

		on Exec3ToAS2 from Exec3 to AS2
			provided(PedalPosition != 0 && WheelSpeed == GroundSpeed && !LossBrakeCMD)
			do{BrakeAntiSkidCMD = true; AntiSkidCmd = true;}

		on sendBRKASCMD from AS1 to AS3
			do { printf("Fault trace: BSCU send brake anti-skid command\n"); }

		on sendBRKASCMD from AS2 to AS3
			do { printf("Fault trace: BSCU send brake anti-skid command\n"); }

		on sendAntiSkidCmd from AS3 to Exec1
			do { printf("Fault trace: BSCU send anti-skid command\n"); }

		// invariants

		// Priorities
	end

	atom type WheelSpeedProvider()
		// Variable declarations
		data int WheelSpeed

		// Port declarations
		export port WBSPort_t1 sendWSpeed ( WheelSpeed )

		// Clock declarations

		// Locations declaration
		place s0

		// Initial location declarations
		initial to s0 do {WheelSpeed = 8;}

		// transitions
		on sendWSpeed from s0 to s0

		// invariants

		// Priorities
	end

	atom type PedalPositionProvider()
		// Variable declarations
		data int PedalPosition

		// Port declarations
		export port WBSPort_t1 sendPP ( PedalPosition )

		// Clock declarations

		// Locations declaration
		place s0

		// Initial location declarations
		initial to s0 do {PedalPosition = 5;}

		// transitions
		on sendPP from s0 to s0

		// invariants

		// Priorities
	end

	atom type GroundSpeedProvider()
		// Variable declarations
		data int GroundSpeed

		// Port declarations
		export port WBSPort_t1 sendGSpeed ( GroundSpeed )

		// Clock declarations

		// Locations declaration
		place s0

		// Initial location declarations
		initial to s0 do {GroundSpeed = 8;}

		// transitions
		on sendGSpeed from s0 to s0 

		// invariants

		// Priorities
	end

	// Atom type for Power Supply
	atom type PowerSupply_Normal()
		// Variable declarations
		data bool Power
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 sendPWR ( Power )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { Power = true; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 0)
			do { Power = true; }

		internal from Init2 to Init
			provided(ManageSignal != 0)

		on sendPWR from Exec to Init

		// invariants

		// Priorities
	end

	atom type PowerSupply_Loss()
		// Variable declarations
		data bool Power
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 sendPWR ( Power )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { Power = true; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 1)
			do { Power = false; }

		internal from Init2 to Init
			provided(ManageSignal != 1)

		on sendPWR from Exec to Init

		// invariants

		// Priorities
	end

	atom type BSCU_ModuleOne()
		// Variable declarations
		data bool LossBrakeCMD
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 sendBSCUFault ( LossBrakeCMD )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { LossBrakeCMD = true; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 0)
			do { LossBrakeCMD = true; }

		internal from Init2 to Init
			provided(ManageSignal != 0)

		on sendBSCUFault from Exec to Init

		// invariants

		// Priorities
	end

	atom type BSCU_ModuleOne_Fault()
		// Variable declarations
		data bool LossBrakeCMD
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 sendBSCUFault ( LossBrakeCMD )
		export port WBSPort_t1 receiveManager (ManageSignal)

		// Clock declarations

		// Locations declaration
		place Exec, Init, Init2

		// Initial location declarations
		initial to Init do { LossBrakeCMD = true; }

		// transitions
		on receiveManager from Init to Init2

		internal from Init2 to Exec
			provided(ManageSignal == 1)
			do { LossBrakeCMD = false; }

		internal from Init2 to Init
			provided(ManageSignal != 1)

		on sendBSCUFault from Exec to Init

		// invariants

		// Priorities
	end

	atom type Wheel()
		// Variable declarations
		data int WheelPressure
		data int MeterHydraulic

		// Port declarations
		export port WBSPort_t1 receiveMeterHyd ( MeterHydraulic )
		export port WBSPort_t1 sendWheelPressure ( WheelPressure )
		port Internal_Port ExecToS1()

		// Clock declarations

		// Locations declaration
		place Init, Exec, S1

		// Initial location declarations
		initial to Init do { WheelPressure = 0; }

		// transitions
		on receiveMeterHyd from Init to Exec

		on ExecToS1 from Exec to S1
			do { WheelPressure = MeterHydraulic; }

		on sendWheelPressure from S1 to Init

		// invariants

		// Priorities
	end

	atom type WheelPressureMonitor()
		// Variable declarations
		data int WheelPressure

		// Port declarations
		export port WBSPort_t1 receiveWheelPressure ( WheelPressure )

		// Clock declarations

		// Locations declaration
		place s0

		// Initial location declarations
		initial to s0

		// transitions
		on receiveWheelPressure from s0 to s0
			do { printf("The output is %d\n",WheelPressure); }

		// invariants

		// Priorities
	end

	atom type AntiSkidValve()
		// Variable declarations
		data int AntiSkidHydraulic
		data int Hydraulic
		data bool AntiSkidCmd
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveASCmd ( AntiSkidCmd )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendASHyd ( AntiSkidHydraulic )
		export port WBSPort_t1 receiveManager( ManageSignal )
		port Internal_Port ExecToS1()
		port Internal_Port ExecToS2()

		// Clock declarations

		// Locations declaration
		place Init, R1, R2, Exec, S1, S2

		// Initial location declarations
		initial to Init do { AntiSkidHydraulic = 0; }

		// transitions
		on receiveASCmd from Init to R1

		on receiveHydraulic from R1 to R2

		on receiveManager from R2 to Exec

		on ExecToS1 from Exec to S1
			provided  ( AntiSkidCmd )
			do { AntiSkidHydraulic = Hydraulic; }

		on ExecToS2 from Exec to S2
			provided  ( !AntiSkidCmd )
			do { AntiSkidHydraulic = 0; }

		on sendASHyd from S1 to Init

		on sendASHyd from S2 to Init

		// invariants

		// Priorities
	end

	atom type AntiSkidValve_StuckOpen()
		// Variable declarations
		data int AntiSkidHydraulic
		data int Hydraulic
		data bool AntiSkidCmd
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveASCmd ( AntiSkidCmd )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendASHyd ( AntiSkidHydraulic )
		export port WBSPort_t1 receiveManager( ManageSignal )
		port Internal_Port ExecToS1()
		port Internal_Port ExecToS2()

		// Clock declarations

		// Locations declaration
		place Init, R1, R2, Exec, S1, S2

		// Initial location declarations
		initial to Init do { AntiSkidHydraulic = 0; }

		// transitions
		on receiveASCmd from Init to R1

		on receiveHydraulic from R1 to R2

		on receiveManager from R2 to Exec

		on ExecToS1 from Exec to S1
			provided  ( AntiSkidCmd )
			do { AntiSkidHydraulic = Hydraulic; }

		on ExecToS2 from Exec to S2
			provided  ( !AntiSkidCmd )
			do { AntiSkidHydraulic = Hydraulic; }

		on sendASHyd from S1 to Init

		on sendASHyd from S2 to Init

		// invariants

		// Priorities
	end

	atom type AntiSkidValve_StuckClose()
		// Variable declarations
		data int AntiSkidHydraulic
		data int Hydraulic
		data bool AntiSkidCmd
		data int ManageSignal

		// Port declarations
		export port WBSPort_t2 receiveASCmd ( AntiSkidCmd )
		export port WBSPort_t1 receiveHydraulic ( Hydraulic )
		export port WBSPort_t1 sendASHyd ( AntiSkidHydraulic )
		export port WBSPort_t1 receiveManager( ManageSignal )
		port Internal_Port ExecToS1()
		port Internal_Port ExecToS2()

		// Clock declarations

		// Locations declaration
		place Init, R1, R2, Exec, S1, S2

		// Initial location declarations
		initial to Init do { AntiSkidHydraulic = 0; }

		// transitions
		on receiveASCmd from Init to R1

		on receiveHydraulic from R1 to R2

		on receiveManager from R2 to Exec

		on ExecToS1 from Exec to S1
			provided  ( AntiSkidCmd )
			do { AntiSkidHydraulic = 0; }

		on ExecToS2 from Exec to S2
			provided  ( !AntiSkidCmd )
			do { AntiSkidHydraulic = 0; }

		on sendASHyd from S1 to Init

		on sendASHyd from S2 to Init

		// invariants

		// Priorities
	end

	atom type SelectorValve()
		// Variable declarations
		data int GreenHydraulicOut
		data int BlueHydraulicOut
		data bool SystemValidity
		data int GreenHydraulicIn
		data int BlueHydraulicIn

		// Port declarations
		export port WBSPort_t2 receiveSYSVal ( SystemValidity )
		export port WBSPort_t1 receiveGreenHyd ( GreenHydraulicIn )
		export port WBSPort_t1 receiveBlueHyd ( BlueHydraulicIn )
		export port WBSPort_t1 sendGreenHyd ( GreenHydraulicOut )
		export port WBSPort_t1 sendBlueHyd ( BlueHydraulicOut )
		port Internal_Port ExecToS1()
		port Internal_Port ExecToS2()
		port Internal_Port ExecToS3()

		// Clock declarations

		// Locations declaration
		place Init, R1, R2, Exec, S1, S2, S3, S4

		// Initial location declarations
		initial to Init do { GreenHydraulicOut = 0; BlueHydraulicOut = 0; }

		// transitions
		on receiveSYSVal from Init to R1
			do { printf("Fault trace: SelectorValve receive system validity\n"); }

		on receiveGreenHyd from R1 to R2
			do { printf("Fault trace: SelectorValve receive green hydraulic\n"); }

		on receiveBlueHyd from R2 to Exec
			do { printf("Fault trace: SelectorValve receive blue hydraulic\n"); }

		on ExecToS1 from Exec to S1
			provided( SystemValidity && GreenHydraulicIn > 0)
			do{ GreenHydraulicOut = GreenHydraulicIn; BlueHydraulicOut = 0; }

		on ExecToS2 from Exec to S2 
			provided( (!SystemValidity || GreenHydraulicIn == 0) && BlueHydraulicIn > 0 )
			do{ BlueHydraulicOut = BlueHydraulicIn; GreenHydraulicOut = 0; }

		on ExecToS3 from Exec to S3
			provided( !(SystemValidity && GreenHydraulicIn > 0) && !((!SystemValidity || GreenHydraulicIn == 0) && BlueHydraulicIn > 0))		
			do { GreenHydraulicOut = 0; BlueHydraulicOut = 0; }

		on sendBlueHyd from S1 to S4
			do { printf("Fault trace: SelectorValve send blue hydraulic\n"); }

		on sendBlueHyd from S2 to S4
			do { printf("Fault trace: SelectorValve send blue hydraulic\n"); }

		on sendBlueHyd from S3 to S4
			do { printf("Fault trace: SelectorValve send blue hydraulic\n"); }

		on sendGreenHyd from S4 to Init
			do { printf("Fault trace: SelectorValve send green hydraulic\n"); }

		// invariants

		// Priorities
	end

  	compound type AntiSkidValveCompound()

		// Variable declarations

		// Component instantiations
		component AntiSkidValve AntiSkidValve()
		component AntiSkidValve_StuckOpen AntiSkidValve_StuckOpen()
		component AntiSkidValve_StuckClose AntiSkidValve_StuckClose()
		component Valve_Manager Valve_Manager()

		// Connector instantiations
		connector getSYSVal connect1( AntiSkidValve.receiveASCmd, AntiSkidValve_StuckOpen.receiveASCmd, AntiSkidValve_StuckClose.receiveASCmd)
		connector sendASHyd connect2( AntiSkidValve.sendASHyd, AntiSkidValve_StuckOpen.sendASHyd, AntiSkidValve_StuckClose.sendASHyd)
		connector transFaultManageSig connect3( Valve_Manager.sendManager, AntiSkidValve.receiveManager, AntiSkidValve_StuckOpen.receiveManager, AntiSkidValve_StuckClose.receiveManager)
		connector getHydraulic connect4( AntiSkidValve.receiveHydraulic, AntiSkidValve_StuckOpen.receiveHydraulic, AntiSkidValve_StuckClose.receiveHydraulic)

		export port connect1.receiveSignal as receiveASCmd
		export port connect2.sendSignal as sendASHyd
		export port connect4.sendSignal as receiveHydraulic
		// Priority instantiations
	end

	// Add compound type for BSCU
	compound type BSCUModuleOne()

		// Variable declarations

		// Component instantiations
		component BSCU_ModuleOne BSCU_ModuleOne()
		component BSCU_ModuleOne_Fault BSCU_ModuleOne_Fault()
		component ValveManager_OneState ValveManager_OneState()

		// Connector instantiations
		connector sendBSCUFault connect1( BSCU_ModuleOne.sendBSCUFault, BSCU_ModuleOne_Fault.sendBSCUFault)
		connector transFaultManageSig_v2 connect2( ValveManager_OneState.sendManager, BSCU_ModuleOne.receiveManager, BSCU_ModuleOne_Fault.receiveManager)

		export port connect1.sendSignal as sendBSCUFault

		// Priority instantiations
	end

	compound type BSCU()

		// Port declarations

		// Variable declarations

		// Component instantiations
		component BSCUModuleOne BSCUModuleOne()
		component BSCU_ModuleTwo  BSCU_ModuleTwo()

		// Connector instantiations
		connector transCmd connect1(BSCUModuleOne.sendBSCUFault, BSCU_ModuleTwo.receiveBSCUFault)
		connector getPWR connect2 (BSCU_ModuleTwo.receivePWR)
		connector getSensor connect3 (BSCU_ModuleTwo.receiveGSpeed)
		connector getSensor connect4 (BSCU_ModuleTwo.receiveWSpeed)
		connector getSensor connect5 (BSCU_ModuleTwo.receivePP)
		connector sendSYSVal connect6 (BSCU_ModuleTwo.sendSYSVal)
		connector sendBRKASCMD connect7 (BSCU_ModuleTwo.sendBRKASCMD)
		connector sendAntiSkidCmd connect8 ( BSCU_ModuleTwo.sendAntiSkidCmd )

		// Priority instantiations
		export port connect2.receiveSignal as receivePWR
		export port connect3.receiveSignal as receiveGSpeed
		export port connect4.receiveSignal as receiveWSpeed
		export port connect5.receiveSignal as receivePP
		export port connect6.sendSignal as sendSYSVal
		export port connect7.sendSignal as sendBRKASCMD
		export port connect8.sendSignal as sendAntiSkidCmd
	end

	// Add compound type for Power Supply 
	compound type PowerSupply()

		// Variable declarations

		// Component instantiations
		component PowerSupply_Normal PowerSupply_Normal()
		component PowerSupply_Loss PowerSupply_Loss()
		component ValveManager_OneState ValveManager_OneState()

		// Connector instantiations
		connector sendPWR connect1( PowerSupply_Normal.sendPWR, PowerSupply_Loss.sendPWR)
		connector transFaultManageSig_v2 connect2( ValveManager_OneState.sendManager, PowerSupply_Normal.receiveManager, PowerSupply_Loss.receiveManager)

		export port connect1.sendSignal as sendPWR

		// Priority instantiations
	end

	compound type HydraulicPumpCompound()

		// Variable declarations

		// Component instantiations
		component HydraulicPump HydraulicPump()
		component HydraulicPump_Loss HydraulicPump_Loss()
		component ValveManager_OneState ValveManager_OneState()

		// Connector instantiations
		connector sendHydraulic connect1( HydraulicPump.sendHydraulic, HydraulicPump_Loss.sendHydraulic)
		connector transFaultManageSig_v2 connect2( ValveManager_OneState.sendManager, HydraulicPump.receiveManager, HydraulicPump_Loss.receiveManager)

		export port connect1.sendSignal as sendHydraulic

		// Priority instantiations
	end

	compound type ShutoffValveCompound()

		// Variable declarations

		// Component instantiations
		component ShutoffValve ShutoffValve()
		component ShutoffValve_StuckOpen ShutoffValve_StuckOpen()
		component ShutoffValve_StuckClose ShutoffValve_StuckClose()
		component Valve_Manager Valve_Manager()

		// Connector instantiations
		connector getSYSVal connect1( ShutoffValve.receiveSYSVal, ShutoffValve_StuckOpen.receiveSYSVal, ShutoffValve_StuckClose.receiveSYSVal)
		connector sendSOHyd connect2( ShutoffValve.sendSOHyd, ShutoffValve_StuckOpen.sendSOHyd, ShutoffValve_StuckClose.sendSOHyd)
		connector transFaultManageSig connect3( Valve_Manager.sendManager, ShutoffValve.receiveManager, ShutoffValve_StuckOpen.receiveManager, ShutoffValve_StuckClose.receiveManager)
		connector getHydraulic connect4( ShutoffValve.receiveHydraulic, ShutoffValve_StuckOpen.receiveHydraulic, ShutoffValve_StuckClose.receiveHydraulic)

		export port connect1.receiveSignal as receiveSYSVal
		export port connect2.sendSignal as sendSOHyd
		export port connect4.sendSignal as receiveHydraulic
		// Priority instantiations
	end

	compound type MeterValveCompound()

		// Variable declarations

		// Component instantiations
		component MeterValve MeterValve()
		component MeterValve_StuckOpen MeterValve_StuckOpen()
		component MeterValve_StuckClose MeterValve_StuckClose()
		component Valve_Manager Valve_Manager()

		// Connector instantiations
		connector getBRKASCMD connect1( MeterValve.receiveBRKASCMD, MeterValve_StuckOpen.receiveBRKASCMD, MeterValve_StuckClose.receiveBRKASCMD)
		connector sendMeterHyd connect2(MeterValve.sendMeterHyd, MeterValve_StuckOpen.sendMeterHyd, MeterValve_StuckClose.sendMeterHyd)
		connector transFaultManageSig connect3( Valve_Manager.sendManager, MeterValve.receiveManager, MeterValve_StuckOpen.receiveManager, MeterValve_StuckClose.receiveManager)
		connector getSOHyd connect4( MeterValve.receiveSOHyd, MeterValve_StuckOpen.receiveSOHyd, MeterValve_StuckClose.receiveSOHyd)
		
		export port connect1.receiveSignal as receiveBRKASCMD
		export port connect2.sendSignal as sendMeterHyd
		export port connect4.sendSignal as receiveHyd


		// Priority instantiations
	end

	// Compound types definitions
	compound type Compound()

		// Variable declarations

		// Component instantiations
		component WheelSpeedProvider WheelSpeedProvider()
		component PedalPositionProvider PedalPositionProvider()
		component GroundSpeedProvider GroundSpeedProvider()
		component PowerSupply PowerSupply()
		component BSCU BSCU()
		component HydraulicPumpCompound GreenHydraulicPump()
		component HydraulicPumpCompound BlueHydraulicPump()
		component HydraulicPumpCompound Accumulator()
		component ShutoffValveCompound ShutoffValveCompound()
		component SelectorValve SelectorValve()
		component AntiSkidValveCompound AntiSkidValveCompound()
		component MeterValveCompound GreenMeterValveCompound()
		component MeterValveCompound BlueMeterValveCompound()
		component Wheel Wheel()
		component WheelPressureMonitor WheelPressureMonitor()

		// Connector instantiations
		connector transHyd connect1( WheelSpeedProvider.sendWSpeed, BSCU.receiveWSpeed)
		connector transHyd connect2( PedalPositionProvider.sendPP, BSCU.receivePP)
		connector transHyd connect3( GroundSpeedProvider.sendGSpeed, BSCU.receiveGSpeed)
		connector transCmd connect9( PowerSupply.sendPWR, BSCU.receivePWR )
		connector transFaultManageSig_v3 connect10( BSCU.sendSYSVal, ShutoffValveCompound.receiveSYSVal, SelectorValve.receiveSYSVal )
		connector transCmd connect11( BSCU.sendAntiSkidCmd, AntiSkidValveCompound.receiveASCmd )
		connector transFaultManageSig_v3 connect12( BSCU.sendBRKASCMD, GreenMeterValveCompound.receiveBRKASCMD, BlueMeterValveCompound.receiveBRKASCMD )
		connector transHyd Hydraulic_GreenPump_To_Shutoff( GreenHydraulicPump.sendHydraulic, ShutoffValveCompound.receiveHydraulic)
		connector transHyd_max Hydraulic_BluePump_Accumulator_To_Selector( BlueHydraulicPump.sendHydraulic, Accumulator.sendHydraulic, SelectorValve.receiveBlueHyd)
		connector transHyd connect7( ShutoffValveCompound.sendSOHyd, SelectorValve.receiveGreenHyd )
		connector transHyd Hydraulic_Selector_To_GreenMeter( SelectorValve.sendGreenHyd, GreenMeterValveCompound.receiveHyd)
		connector transHyd Hydraulic_Selector_To_AntiSkid( SelectorValve.sendBlueHyd, AntiSkidValveCompound.receiveHydraulic)
		connector transHyd Hydraulic_AntiSkid_To_BlueMeter( AntiSkidValveCompound.sendASHyd, BlueMeterValveCompound.receiveHyd)
		connector transHyd_v2 Hydraulic_GreenMeter_BlueMeter_To_Wheel( GreenMeterValveCompound.sendMeterHyd, BlueMeterValveCompound.sendMeterHyd, Wheel.receiveMeterHyd)
		connector transHyd Pressure_Wheel_To_Monitor( Wheel.sendWheelPressure, WheelPressureMonitor.receiveWheelPressure)
		// Priority instantiations
	end
end